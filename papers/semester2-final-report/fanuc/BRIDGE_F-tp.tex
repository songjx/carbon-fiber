\begin{verbatim}
 1:  UFRAME_NUM=4                            // set user frame
 2:  UTOOL_NUM=4                             // set user tool frame
 3:  R[4:layer num]=0                        // reset layer counter
 4:  PR[12:layer frame RW]=                  // set layer RW (read+write) frame 
  :  PR[11:layer frame R]                       position register   
 5:  R[5:contour num]=0                      // reset contour counter
 6:  PR[13,2:contour offset]=0               // reset contour offset
 7:  R[7:contour incr dir]=(-1)              // reset contour increment direction
 8:  TOOL_OFFSET_CONDITION                   // set tool offset condition to  
  :  PR[13:contour offset]                      contour offset
 9:  LBL[2]                                  // label 2: layer loop
10:  UFRAME[4]=PR[12:layer frame RW]         // set user frame to incremented 
  :                                             layer frame
11:  LBL[1]                                  // label 1: contour loop
12:J  P[1] 100% FINE Tool_Offset             // joint move to point 1
13:J  P[2] 100% FINE Tool_Offset
14:C  P[3] Tool_Offset                       // circular interpolation 
  :   P[4] 250mm/sec FINE Tool_Offset           thru pt 3 to pt 4
15:C  P[5] Tool_Offset
  :   P[6] 250mm/sec FINE Tool_Offset
16:C  P[7] Tool_Offset
  :   P[8] 250mm/sec FINE Tool_Offset
17:J  P[9] 100% FINE Tool_Offset
18:  PR[13,2:contour offset]=(               // increment contour offset 
  :  PR[13,2:contour offset]+                   position register
  :  R[6:contour incremnt]*
  :  R[7:contour incr dir]))
19:J  P[9] 100% FINE Tool_Offset
20:C  P[8] Tool_Offset
  :   P[7] 250mm/sec FINE Tool_Offset
21:C  P[6] Tool_Offset
  :   P[5] 250mm/sec FINE Tool_Offset
22:C  P[4] Tool_Offset
  :   P[3] 250mm/sec FINE Tool_Offset
23:J  P[2] 100% FINE Tool_Offset
24:J  P[1] 100% FINE Tool_Offset
25:  R[5:contour num]=R[5:contour num]+1     // increment contour counter
26:  IF R[5:contour num]>=31,JMP LBL[3]      // don't increment after last 
                                                contour pair
27:  PR[13,2:contour offset]=(
  :  PR[13,2:contour offset]+
  :  R[6:contour incremnt]*
  :  R[7:contour incr dir]))
28:  JMP LBL[1]
29:  LBL[3]
30:  R[4:layer num]=R[4:layer num]+1         // increment layer counter
31:  PR[12,3:layer frame RW]=                // increment layer frame 
  :  PR[12,3:layer frame RW]+.1                 position register
32:  R[7:contour incr dir]=                  // invert contour increment
  :  R[7:contour incr dir]*(-1)                 direction
33:  IF R[4:layer num]<31,JMP LBL[2]
[END]
\end{verbatim}
